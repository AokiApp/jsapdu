buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['JsapduRn_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
    // Formatting and static analysis plugins
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
    classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.6"
  }
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply from: '../nitrogen/generated/android/aokiapp_jsapdurn+autolinking.gradle'

apply plugin: "com.facebook.react"
apply plugin: "com.diffplug.spotless"
apply plugin: "io.gitlab.arturbosch.detekt"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["JsapduRn_" + name]).toInteger()
}

android {
  namespace "app.aoki.jsapdu.rn"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

    externalNativeBuild {
      cmake {
        cppFlags "-frtti -fexceptions -Wall -fstack-protector-all"
        arguments "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
        abiFilters (*reactNativeArchitectures())

        buildTypes {
          debug {
            cppFlags "-O1 -g"
          }
          release {
            cppFlags "-O2"
          }
        }
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  packagingOptions {
    excludes = [
            "META-INF",
            "META-INF/**",
            "**/libc++_shared.so",
            "**/libfbjni.so",
            "**/libjsi.so",
            "**/libfolly_json.so",
            "**/libfolly_runtime.so",
            "**/libglog.so",
            "**/libhermes.so",
            "**/libhermes-executor-debug.so",
            "**/libhermes_executor.so",
            "**/libreactnative.so",
            "**/libreactnativejni.so",
            "**/libturbomodulejsijni.so",
            "**/libreact_nativemodule_core.so",
            "**/libjscexecutor.so"
    ]
  }

  buildFeatures {
    buildConfig true
    prefab true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
    abortOnError true
    warningsAsErrors true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
      // Exclude React Native core generated specs and viewmanagers from this library
      java {
        exclude 'com/facebook/fbreact/specs/**'
        exclude 'com/facebook/react/viewmanagers/**'
      }
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  // Do not package RN core into this AAR; let the app provide it to avoid duplicate classes
  compileOnly "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation project(":react-native-nitro-modules")
}

// Formatting: Spotless with ktlint
spotless {
  kotlin {
    target "src/**/*.kt"
    ktlint("1.2.1").editorConfigOverride([
      "ktlint_code_style": "android_studio",
      "indent_size": "2",
      "insert_final_newline": "true",
      "max_line_length": "120",
      "ktlint_standard_no-wildcard-imports": "disabled"
    ])
  }
}

// Static analysis: Detekt (defaults)
detekt {
  buildUponDefaultConfig = true
  allRules = false
  source = files("src/main/java")
  parallel = true
}

// Syntax/type check: compile Kotlin (without producing AAR)
tasks.register("libraryTypeCheck") {
  group = "verification"
  description = "Compile Kotlin sources to perform syntax/type checking for the RN library module"
  dependsOn("compileDebugKotlin")
}
